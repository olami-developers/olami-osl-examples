#PowerGrammar 1.0;
!slot <number:number(1|3)>;
!slot <單字動詞1:ext(1|1)>;
!slot <who_slot1:ext(1|5)>;
!slot <description:ext(1|4)>;
!slot <number1:number(1|2)>;
!slot <number2:number(1|2)>;
!slot <taiwanhome:internal(1|2)>;
!slot <body:internal(1|3)>;
!slot <稱謂:internal(1|3)>;
!slot <單字動詞2:ext(1|1)>;
!slot <food:internal(2|2)>;
!slot <haweioneword:ext(1|1)>;
!intention <示範>;

<示範>:<阿嶽真的很嚴格>|
    <我藍瘦香菇>|
    <你能否x好x滿>|
    <donottouchmybody>|
    <hawei>|
    <你知道藍瘦香菇嗎>|
    <台灣我的主場>|
    <測試用的你好>|
    <娘子快跟牛魔王出來看上帝>|
    <taipeiuniversiade>|
    <做人如果沒有夢想那跟鹹魚有什麼分別>|
    <用不用這麼認真啊神經病>|
    <franchfries>;
//All rules
<very>:非常|很|十分|超[級]|太;
<quickly>:快[點]|趕快|立刻|馬上|立馬|立即|即刻;
<feel>:覺得|感到|感覺;
<husband>:老公|丈夫|相公|先生;
<body>:肩[膀]|手[臂|指|掌|心|肘]|屁股|臀[部]|臉[頰|蛋]|頭[頂|部]|頭髮|胸[部|膛|口]|脖子|頸部|背[脊]|耳[朵]|下巴|[大|小]腿|腳|眼[睛]|嘴[巴]|鼻[子]|額頭|肚[子]|腹部;
<hope>:(夢|理)想|希望|目標;
<olami>:olami|歐拉蜜|你;
<canornot>:可[不可]以|能[不能]|行[不行]|好[不好]|(能|可)否;
<me>:我|俺|人家;
<if>:(如果|萬一|假(如|設|使)|要是)[說];
<you>:你|您;
<need>:需[要]|用|要;
<like>:喜歡|喜愛;
<wife>:老婆|太太|娘子|妻子;
<藍瘦香菇>:(藍|男|難)(瘦|受)(香菇|想哭);
<touch>:[觸]碰|碰觸|[撫|觸]摸|拍[打]|打;
<please>:請|拜託|麻煩|勞煩|勞駕;
<manpronoun>:你|我|他|她|他們|她們|我們|你們|妳們|妳;
<canyou>:[<please>]<you>[<canornot>]|<canornot>[<please><you>]|<please>;
<know>:知[道]|了[解];
<endyuqi>:啦|吧|呢|嗎|啊|阿|呀|喔|噢|哇|唷|呦|咧|哩|嚕|嘍|囉|了;
<what1>:(甚|什)麼|怎樣|[如]何;
<what2>:是<what1>[意思]|的意思[是<what1>];
<biaodian>:，|。|、|！|？|；|：;
<haweifood>:香蕉<{haweioneword=香}>|哈密瓜<{haweioneword=哈}>|荔枝<{haweioneword=枝}>|草莓<{haweioneword=莓}>|漢堡<{haweioneword=漢}>|滷肉飯<{haweioneword=滷}>;
<saying>:聽|聽說;
<so>:(這|那)(麼|樣)|如此|這般;
<considers>:認[為]|覺[得]|感覺;
<idiot>:(笨|蠢)[蛋]|神經[病]|白目|白(癡|痴)|北七|瘋子;
<yao>:要|行|准|可[以]|能[夠];
<differ>:(分|區|差)別|不(一樣|同)|差;
<reallyornot>:(假|真)的[是]|真[是];
<food>:薯條|布丁|麵包|餅乾;
<with>:跟|和|與;
<time>:日|月|天|年;
<taiwanhome>:(台|臺)灣|(台|臺)北|家;
//All templates
<thistime:template>: [=數字,A=]這$(A)|這$(數字)[個]$(A)[子]|這$(數字)次|這次;
<你聽說_結構:template>: [=主語,謂語=]$(主語)[到底|究竟]<疊式2(<saying>)>$(謂語)|$(謂語)$(主語)[到底|究竟]<疊式2(<saying>)>;
<你知道_結構:template>: [=主語,謂語=]$(主語)[到底|究竟]<疊式1(<know>)>$(謂語)|$(謂語)$(主語)[到底|究竟]<疊式1(<know>)>;
<疊式1:template>: [=動詞=]$(動詞)不$(動詞)|[不]$(動詞);
<知道聽說_短句:template>: [=短句=]<你知道_結構(<manpronoun>,$(短句))>|<你聽說_結構(<manpronoun>,$(短句))>;
<疊式2:template>: [=動詞=]有[沒有]$(動詞)過|$(動詞)|$(動詞)沒$(動詞)過;
<donot:template>: [=動詞=]不$(動詞);
//All grammars
//*C:阿嶽真的很嚴格
//*A:<who_slot1>覺得很普通，我覺得其實可以
//*A:<who_slot1>真的比我想像中嚴格
//*A:你有點兒情緒失控
<阿嶽真的很嚴格>:[<manpronoun>][<疊式1(<considers>)>]<who_slot1>[<reallyornot>+][<very>+]嚴(格|厲|峻)[了][<endyuqi>];
//*C:我藍瘦香菇
//*A:我也覺得藍瘦香菇
<我藍瘦香菇>:[<manpronoun>][<feel>][<very>]<藍瘦香菇>[<endyuqi>];
//*C:請你睡好睡滿
//*C:請睡好睡滿
//*C:吃好喝滿
//*A:放心啦，我一定會<單字動詞1>好<單字動詞2>滿的
//*A:我才不要，你自己去<單字動詞1>好<單字動詞2>滿啦
<你能否x好x滿>:([<please>][<you>][<canornot>]|<canornot><please><you>)<單字動詞1>好<單字動詞2>滿[<canornot>][<endyuqi>];
//*C:不要碰我肩膀
//*A:你是不是搞錯了什麼？我又沒有手，要怎麼碰你<body>啦！
//*A:誰要碰你<body>，碰你一下還會底累我的行程，我明天還要上班咧！
//*A:我又不是迪哥。迪哥你聽得懂嗎？
<donottouchmybody>:[<canyou>][就[是]](<donot(<yao>)>)<touch>[到]<me>[的]
<body>[<canornot>][<endyuqi>];
//*C:哈密瓜有什麼味道
//*A:有一種<haweioneword>味，有的人喜歡有的人不喜歡
//*A:難道是嘉明的味道
//*A:你吃吃看不就知道了
<hawei>:[<manpronoun>[<know>|<considers>]]<haweifood>(是|[是]有)<what1>(味[道]|口味|滋味)[<endyuqi>];
//*C:你知道藍瘦香菇嗎
//*A:就是我現在的心情，藍瘦香菇，難受想哭
<你知道藍瘦香菇嗎>:<知道聽說_短句(<藍瘦香菇>)>[<what2>][<endyuqi>];
//*C:台灣我的主場
//*A:我的主場，我的榮耀。這次，我們回<taiwanhome>比賽。
//*A:你的夢想，算歐拉蜜一份！
//*A:世界歡迎光臨，熱情準備就緒！歐拉蜜已經熱血爆棚啦！
<台灣我的主場>:<taiwanhome>[<biaodian>][[就]是]我[們]的主場;
//*C:123你好
//*C:一二三妳好
//*C:一二三你好
//*A:雷侯雷侯阿
<測試用的你好>:(123|一二三)(你|妳)好;
//*C:娘子快跟牛魔王出來看上帝
//*A:<稱謂>，有神快拜！
//*A:不用看啦，因為<稱謂>就是我的上帝呀！
<娘子快跟牛魔王出來看上帝>:(<wife><{稱謂=老公}>|<husband><{稱謂=老婆}>)
[<biaodian>][<quickly>]<with>牛魔王出來看上帝[<endyuqi>];
//*C:這次，我們回家比賽
//*A:歐拉蜜和大家一起為中華隊加油
//*A:<taiwanhome>就是我的主場
//*A:你的夢想，算我歐拉蜜一份
<taipeiuniversiade>:<thistime(<number>,<time>)>[<biaodian>]<manpronoun>[一起]回<taiwanhome>比賽;
//*C:做人如果沒有夢想那跟鹹魚有什麼分別
//*A:可是我天生就是想當<who_slot1>耶呵呵
//*A:你知道要當<who_slot1>這種東西很講究天分的
//*A:憑你的智慧我很難跟你解釋
<做人如果沒有夢想那跟鹹魚有什麼分別>:([做[為]|當][[一]個]人<if>|<if>[做[為]|當][[一]個]人)(沒[有|了]|無)<hope>[那[麼]]跟<who_slot1>有<what1><differ>[<endyuqi>];
//*C:用不用這麼認真啊神經病
//*A:咦，你怎麼知道的？很多人都是這樣說我的。
//*A:<description>錯了嗎？我媽媽說文明人是不會亂罵人的！
<用不用這麼認真啊神經病>:[<canyou>]<疊式1(<need>)><so><description>[<canornot>][<endyuqi>][<biaodian>]<idiot>;
//*C:三年三班手工薯條超級好吃
//*A:陳志豪還沒請我喝茶湯會
//*A:你全家都是<food>
//*A:陳志豪就叫你不要來了很熱耶
<franchfries>:<number1>年<number2>班(手工|現做)<food><very>+(好吃|美味)[<endyuqi>];
