#PowerGrammar 1.0;
!slot <情境:ext(3|20)>;
!intention <示範>;

<示範>:<我藍瘦香菇>|
    <測試用的你好>|
    <你知道藍瘦香菇嗎>;
//All rules
<very>:非常|很|十分|超[級]|太;
<藍瘦香菇>:(藍|男|難)(瘦|受)(香菇|想哭);
<feel>:覺得|感到|感覺;
<endyuqi>:啊|吧|呢|嗎;
<what1>:什麼|怎麼|如何|怎樣;
<what2>:是<what1>[意思]|的意思[是<what1>];
<me>:你|我|他|她|他們|她們;
<know>:知[道]|了[解];
<saying>:聽|聽說;
//All templates
<你聽說_結構:template>: [=主語,謂語=]$(主語)[到底|究竟]<疊式2(<saying>)>$(謂語)|$(謂語)$(主語)[到底|究竟]<疊式2(<saying>)>;
<疊式1:template>: [=動詞=]$(動詞)不$(動詞)|$(動詞);
<知道聽說_短句:template>: [=短句=]<你知道_結構(<me>,$(短句))>|<你聽說_結構(<me>,$(短句))>;
<疊式2:template>: [=動詞=]有[沒有]$(動詞)過|$(動詞)|$(動詞)沒$(動詞)過;
<你知道_結構:template>: [=主語,謂語=]$(主語)[到底|究竟]<疊式1(<know>)>$(謂語)|$(謂語)$(主語)[到底|究竟]<疊式1(<know>)>;
//All grammars
//*C:我藍瘦香菇
//*A:我也覺得藍瘦香菇
<我藍瘦香菇>:[<情境>]<me>[<feel>][<very>]<藍瘦香菇>[<endyuqi>];
//*C:123你好
//*C:一二三妳好
//*C:一二三你好
//*A:雷侯雷侯阿
<測試用的你好>:(123|一二三)(你|妳)好;
//*C:你知道藍瘦香菇嗎
//*A:就是我現在的心情，藍瘦香菇，難受想哭
<你知道藍瘦香菇嗎>:<知道聽說_短句(<藍瘦香菇>)>[<what2>][<endyuqi>];
