#PowerGrammar 1.0;
!slot <單字動詞1:ext(1|1)>;
!slot <who_slot1:ext(1|5)>;
!slot <單字動詞2:ext(1|1)>;
!slot <稱謂:internal(1|3)>;
!intention <示範>;
!modifier <示範> : eat|啟動;

<示範>:<阿嶽真的很嚴格>|
    <你能否x好x滿>|
    <我藍瘦香菇>|
    <測試用的你好>|
    <娘子快跟牛魔王出來看上帝>|
    <聽歌>|
    <做人如果沒有夢想那跟鹹魚有什麼分別>|
    <你知道藍瘦香菇嗎>|
    <grammar1>|
    <我愛olami>|
    <吃蘋果>;
//All rules
<very>:非常|很|十分|超[級]|太;
<quickly>:快[點]|趕快|立刻|馬上|立馬|立即|即刻;
<feel>:覺得|感到|感覺;
<husband>:老公|丈夫|相公|先生;
<endyuqi>:啦|吧|呢|嗎|啊|阿|呀;
<type>:流行|經典|兒童|搖滾;
<hope>:(夢|理)想|希望|目標;
<olami>:olami|歐拉蜜|你;
<what1>:(甚|什)麼|怎樣|[如]何;
<what2>:是<what1>[意思]|的意思[是<what1>];
<biaodian>:，|。|、|！|？|；|：;
<canornot>:可[不可]以|能[不能]|行[不行]|好[不好]|(能|可)否;
<saying>:聽|聽說;
<if>:(如果|萬一|假(如|設|使)|要是)[說];
<you>:你|您;
<considers>:認[為]|覺[得]|感覺;
<like>:喜歡|喜愛;
<wife>:老婆|太太|娘子|妻子;
<藍瘦香菇>:(藍|男|難)(瘦|受)(香菇|想哭);
<please>:請|拜託|麻煩|勞煩|勞駕;
<manpronoun>:你|我|他|她|他們|她們;
<differ>:(分|區|差)別|不(一樣|同)|差;
<reallyornot>:(假|真)的[是]|真[是];
<with>:跟|和|與;
<know>:知[道]|了[解];
<device>:電視|冷氣|窗簾|投影機;
<open>:打開|開啟|啟動;
//All templates
<疊式1:template>: [=動詞=]$(動詞)不$(動詞)|$(動詞);
<知道聽說_短句:template>: [=短句=]<你知道_結構(<manpronoun>,$(短句))>|<你聽說_結構(<manpronoun>,$(短句))>;
<疊式2:template>: [=動詞=]有[沒有]$(動詞)過|$(動詞)|$(動詞)沒$(動詞)過;
<你聽說_結構:template>: [=主語,謂語=]$(主語)[到底|究竟]<疊式2(<saying>)>$(謂語)|$(謂語)$(主語)[到底|究竟]<疊式2(<saying>)>;
<你知道_結構:template>: [=主語,謂語=]$(主語)[到底|究竟]<疊式1(<know>)>$(謂語)|$(謂語)$(主語)[到底|究竟]<疊式1(<know>)>;
//All grammars
//*C:阿嶽真的很嚴格
//*A:<who_slot1>覺得很普通，我覺得其實可以
//*A:<who_slot1>真的比我想像中嚴格
//*A:你有點兒情緒失控
<阿嶽真的很嚴格>:[<manpronoun>][<疊式1(<considers>)>]<who_slot1>[<reallyornot>+][<very>+]嚴(格|厲|峻)[了][<endyuqi>];
//*C:請你睡好睡滿
//*C:吃好喝滿
//*C:請睡好睡滿
//*A:放心啦，我一定會<單字動詞1>好<單字動詞2>滿的
//*A:我才不要，你自己去<單字動詞1>好<單字動詞2>滿啦
<你能否x好x滿>:([<please>][<you>][<canornot>]|<canornot><please><you>)<單字動詞1>好<單字動詞2>滿[<canornot>][<endyuqi>];
//*C:我藍瘦香菇
//*A:我也覺得藍瘦香菇
<我藍瘦香菇>:[<manpronoun>][<feel>][<very>]<藍瘦香菇>[<endyuqi>];
//*C:一二三你好
//*C:一二三妳好
//*C:123你好
//*A:雷侯雷侯阿
<測試用的你好>:(123|一二三)(你|妳)好;
//*C:娘子快跟牛魔王出來看上帝
//*A:<稱謂>，有神快拜！
//*A:不用看啦，因為<稱謂>就是我的上帝呀！
<娘子快跟牛魔王出來看上帝>:(<wife><{稱謂=老公}>|<husband><{稱謂=老婆}>)[<biaodian>][<quickly>]<with>牛魔王出來看上帝[<endyuqi>];
//*C:我要聽流行歌
<聽歌>:我要聽<type>歌;
//*C:做人如果沒有夢想那跟鹹魚有什麼分別
//*A:可是我天生就是想當<who_slot1>耶呵呵
//*A:你知道要當<who_slot1>這種東西很講究天分的
//*A:憑你的智慧我很難跟你解釋
<做人如果沒有夢想那跟鹹魚有什麼分別>:([做[為]|當][[一]個]人<if>|<if>[做[為]|當][[一]個]人)(沒[有|了]|無)<hope>[那[麼]]跟<who_slot1>有<what1><differ>[<endyuqi>];
//*C:你知道藍瘦香菇嗎
//*A:就是我現在的心情，藍瘦香菇，難受想哭
<你知道藍瘦香菇嗎>:<知道聽說_短句(<藍瘦香菇>)>[<what2>][<endyuqi>];
//*C:請幫我打開電視
<grammar1>:請幫我<open><device><{@=啟動}>;
//*C:我愛OLAMI
<我愛olami>:我愛<olami>;
//*C:我非常喜歡吃蘋果
<吃蘋果>:我(很|非常)<like>吃蘋果<{@=eat}>;
